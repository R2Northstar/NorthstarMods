untyped

global function CHudChat_ProcessMessageStartThread
global function AddCallback_OnReceivedSayTextMessage

global struct ClClient_MessageStruct {
	string message
	entity player
	bool isTeam
    bool isDead
    bool isWhisper
	bool shouldBlock
}

struct {
	array< ClClient_MessageStruct functionref( ClClient_MessageStruct ) > OnReceivedSayTextMessageCallbacks
} NsCustomCallbacksClient

void function OnReceivedMessage(ClClient_MessageStruct localMessage) {
    if (localMessage.player != null)
    {
        foreach (callbackFunc in NsCustomCallbacksClient.OnReceivedSayTextMessageCallbacks)
        {
            localMessage = callbackFunc(localMessage)

            if (localMessage.shouldBlock)
            {
                return
            }
        }
    }

    NSChatWriteRaw(1, "\n")

    if (localMessage.player == null) NSChatWrite(1, "\x1b[95m")
    else
    {
        bool isFriendly = localMessage.player.GetTeam() == GetLocalClientPlayer().GetTeam()

        if (isFriendly) NSChatWrite(1, "\x1b[111m")
        else NSChatWrite(1, "\x1b[112m")
    }

    if (localMessage.isWhisper) NSChatWriteRaw(1, Localize("#HUD_CHAT_WHISPER_PREFIX"))
    if (localMessage.isDead) NSChatWriteRaw(1, Localize("#HUD_CHAT_DEAD_PREFIX"))
    if (localMessage.isTeam) NSChatWriteRaw(1, Localize("#HUD_CHAT_TEAM_PREFIX"))

    if (localMessage.player == null) 
    {
        NSChatWriteRaw(1, Localize("#HUD_CHAT_SERVER_PREFIX") + " ")
    }
    else {
        NSChatWriteRaw(1, localMessage.player.GetPlayerName())
        NSChatWriteRaw(1, ": ")
    }

    NSChatWrite(1, "\x1b[0m")

    NSChatWrite(1, localMessage.message)
}

void function CHudChat_ProcessMessageStartThread()
{
	thread CHudChat_OnReceivedSayTextMessageCallback()
}

void function CHudChat_OnReceivedSayTextMessageCallback()
{
    int fromPlayerIndex = NSChatGetCurrentPlayer()
    entity fromPlayer = null

    if (fromPlayerIndex >= 0 && fromPlayerIndex < GetPlayerArray().len())
    {
        fromPlayer = GetPlayerArray()[fromPlayerIndex]
    }

    string message = NSChatGetCurrentMessage()
    bool isTeam = NSChatGetIsTeam()
    bool isDead = NSChatGetIsDead()
    int messageType = NSChatGetCurrentType()

    if (messageType == 0 || messageType == 1)
    {
        ClClient_MessageStruct localMessage
        localMessage.message = message
        localMessage.player = fromPlayer
        localMessage.isTeam = isTeam
        localMessage.isDead = isDead
        localMessage.isWhisper = false
        localMessage.shouldBlock = false
        OnReceivedMessage(localMessage)
        return
    }

    if (messageType == 2)
    {
        ClClient_MessageStruct localMessage
        localMessage.message = message
        localMessage.player = fromPlayer
        localMessage.isTeam = isTeam
        localMessage.isDead = isDead
        localMessage.isWhisper = true
        localMessage.shouldBlock = false
        OnReceivedMessage(localMessage)
        return
    }
}

void function AddCallback_OnReceivedSayTextMessage( ClClient_MessageStruct functionref (ClClient_MessageStruct) callbackFunc )
{
	NsCustomCallbacksClient.OnReceivedSayTextMessageCallbacks.append(callbackFunc)
}
