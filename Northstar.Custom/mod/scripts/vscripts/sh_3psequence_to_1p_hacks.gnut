global function FirstPersonSequenceForce1P_Init

#if SERVER
	global function FirstPersonSequenceForce1P
	global function ShouldRunFirstPersonSequence
#endif

#if CLIENT
	global function ServerCallback_HideHudForFPHackAnim
#endif

global const string FORCE1P_PILOT_1P_ATTACHMENT = "HEADFOCUS"
global const string FORCE1P_TITAN_1P_ATTACHMENT = "HATCH_HEAD"

global const string FORCE1P_PILOT_1P_HIDDEN_BODYGROUP = "head"
global const string FORCE1P_TITAN_1P_HIDDEN_BODYGROUP = "torso"

global const string FORCE1P_PILOT_ENTITYCLASS = "npc_pilot_elite"
global const string FORCE1P_TITAN_ENTITYCLASS = "npc_titan"

global struct Forced1PSequenceData
{
	entity player
	entity camera
	entity ownerProxy
	entity thirdPersonProxy
}

void function FirstPersonSequenceForce1P_Init()
{
	AddCallback_OnRegisteringCustomNetworkVars( FirstPersonSequenceForce1P_RegisterCustomNetworkFunctions )
}

void function FirstPersonSequenceForce1P_RegisterCustomNetworkFunctions()
{
	Remote_RegisterFunction( "ServerCallback_HideHudForFPHackAnim" )
}

#if SERVER
Forced1PSequenceData function FirstPersonSequenceForce1P( FirstPersonSequenceStruct sequence, entity player, entity other = null )
{
	string attachment = FORCE1P_PILOT_1P_ATTACHMENT
	string hiddenBodygroup = FORCE1P_PILOT_1P_HIDDEN_BODYGROUP
	string entityclass = FORCE1P_PILOT_ENTITYCLASS

	if ( player.IsTitan() )
	{
		attachment = FORCE1P_TITAN_1P_ATTACHMENT
		hiddenBodygroup = FORCE1P_TITAN_1P_HIDDEN_BODYGROUP	
		entityclass	= FORCE1P_TITAN_ENTITYCLASS
	}

	player.Hide()

	Forced1PSequenceData cleanupData
	cleanupData.player = player

	vector angles = player.GetAngles()

	angles.y = player.EyeAngles().y

	entity ownerProxy

	if ( entityclass == FORCE1P_TITAN_ENTITYCLASS )
		ownerProxy = CreateNPCTitan( player.GetPlayerSettings(), player.GetTeam(), <0, 0, 0>, <0, 0, 0> )
	else
		ownerProxy = CreateEntity( entityclass )

	ownerProxy.SetOrigin( player.GetOrigin() )

	if ( Distance( angles, angles ) < 359 )
		ownerProxy.SetAngles( angles )

	ownerProxy.kv.VisibilityFlags = ENTITY_VISIBLE_TO_OWNER
	ownerProxy.kv.solid = 0
	ownerProxy.SetOwner( player )

	DispatchSpawn( ownerProxy )

	if ( ownerProxy.GetModelName() != player.GetModelName() )
	{
		ownerProxy.SetModel( player.GetModelName() )
		ownerProxy.SetValueForModelKey( player.GetModelName() )
	}

	ownerProxy.EnableNPCFlag( NPC_IGNORE_ALL )
	ownerProxy.SetBossPlayer( player )

	NPC_NoTarget( ownerProxy )
	SetTeam( ownerProxy, player.GetTeam() )
	TakeWeaponsForArray( ownerProxy, ownerProxy.GetMainWeapons() )

	ownerProxy.SetSkin( player.GetSkin() )
	ownerProxy.SetCamo( player.GetCamo() )
	ownerProxy.SetDecal( player.GetDecal() )
	ownerProxy.SetInvulnerable()

	HideName( ownerProxy )

	cleanupData.ownerProxy = ownerProxy

	if ( ownerProxy.IsTitan() && HasSoul( ownerProxy ) )
	{
		entity ownerProxysoul = ownerProxy.GetTitanSoul()

		if ( IsValid( ownerProxysoul ) )
			ownerProxysoul.SetShieldHealth( ownerProxysoul.GetShieldHealthMax() )
	}
	else
	{
		AddAnimEvent( ownerProxy, "phase_shift_start", ModdedPhaseEmbarkPhaseStart )
		AddAnimEvent( ownerProxy, "phase_shift_stop", ModdedPhaseEmbarkPhaseStop )
		if ( !IsPlayerEmbarking( player ) )
		{
			AddAnimEvent( ownerProxy, "phase_shift_explode", ModdedPhaseShiftExplodeSound )
			AddAnimEvent( ownerProxy, "cloak_on", ModdedAnimEvent_Cloak_On )
			AddAnimEvent( ownerProxy, "cloak_off", ModdedAnimEvent_Cloak_Off )
		}
	}

	int bodygroupValue = 1

	if ( hiddenBodygroup == "torso" )
		bodygroupValue = 2

	if ( ownerProxy.FindBodyGroup( hiddenBodygroup ) != -1 )
		ownerProxy.SetBodygroup( ownerProxy.FindBodyGroup( hiddenBodygroup ), bodygroupValue )

	entity thirdPersonProxy

	if ( entityclass == FORCE1P_TITAN_ENTITYCLASS )
		thirdPersonProxy = CreateNPCTitan( player.GetPlayerSettings(), player.GetTeam(), <0, 0, 0>, <0, 0, 0> )
	else
		thirdPersonProxy = CreateEntity( entityclass )

	thirdPersonProxy.SetOrigin( player.GetOrigin() )

	if ( Distance( angles, angles ) < 359 )
		thirdPersonProxy.SetAngles( angles )

	thirdPersonProxy.kv.VisibilityFlags = ENTITY_VISIBLE_TO_EVERYONE & ~ENTITY_VISIBLE_TO_OWNER
	thirdPersonProxy.kv.solid = 0
	thirdPersonProxy.SetOwner( player )

	DispatchSpawn( thirdPersonProxy	)

	if ( thirdPersonProxy.GetModelName() != player.GetModelName() )
	{
		thirdPersonProxy.SetModel( player.GetModelName() )
		thirdPersonProxy.SetValueForModelKey( player.GetModelName() )
	}

	thirdPersonProxy.EnableNPCFlag( NPC_IGNORE_ALL )
	thirdPersonProxy.SetBossPlayer( player )

	NPC_NoTarget( thirdPersonProxy )
	SetTeam( thirdPersonProxy, player.GetTeam() )
	TakeWeaponsForArray( thirdPersonProxy, thirdPersonProxy.GetMainWeapons() )

	thirdPersonProxy.SetSkin( player.GetSkin() )
	thirdPersonProxy.SetCamo( player.GetCamo() )
	thirdPersonProxy.SetDecal( player.GetDecal() )
	thirdPersonProxy.SetInvulnerable()

	HideName( thirdPersonProxy )

	cleanupData.thirdPersonProxy = thirdPersonProxy

	if ( thirdPersonProxy.IsTitan() )
	{
		Highlight_SetEnemyHighlight( thirdPersonProxy, "enemy_titan" )

		if ( HasSoul( thirdPersonProxy ) )
		{
			entity thirdPersonProxySoul = thirdPersonProxy.GetTitanSoul()

			if ( IsValid( thirdPersonProxySoul ) )
				thirdPersonProxySoul.SetShieldHealth( thirdPersonProxySoul.GetShieldHealthMax() )
		}
	}
	else
	{
		Highlight_SetEnemyHighlight( thirdPersonProxy, "enemy_player" )
		AddAnimEvent( thirdPersonProxy, "phase_shift_start", ModdedPhaseEmbarkPhaseStart )
		AddAnimEvent( thirdPersonProxy, "phase_shift_stop", ModdedPhaseEmbarkPhaseStop )
		if ( !IsPlayerEmbarking( player ) )
		{
			AddAnimEvent( thirdPersonProxy, "phase_shift_explode", ModdedPhaseShiftExplodeSound )
			AddAnimEvent( thirdPersonProxy, "cloak_on", ModdedAnimEvent_Cloak_On )
			AddAnimEvent( thirdPersonProxy, "cloak_off", ModdedAnimEvent_Cloak_Off )
		}
	}

	thread SetBodyGroups( player, thirdPersonProxy )
	FirstPersonPlayAnim( sequence, thirdPersonProxy, player, other )

	entity camera = CreateEntity( "point_viewcontrol" )
	camera.SetParent( ownerProxy, attachment )
	camera.kv.spawnflags = 56
	DispatchSpawn( camera )
	player.SetViewEntity( camera, true )
	cleanupData.camera = camera

	Remote_CallFunction_NonReplay( player, "ServerCallback_HideHudForFPHackAnim" )
	thread CleanupForced1PSequenceAfterAnimDone( sequence, ownerProxy, other, cleanupData )

	return cleanupData
}

void function CleanupForced1PSequenceAfterAnimDone( FirstPersonSequenceStruct sequence, entity player, entity other, Forced1PSequenceData cleanupData )
{
	player.EndSignal( "OnDeath" )
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnAnimationInterrupted" )
	player.EndSignal( "OnAnimationDone" )

	cleanupData.player.EndSignal( "OnDeath" )
	cleanupData.player.EndSignal( "OnDestroy" )
	cleanupData.player.EndSignal( "OnAnimationInterrupted" )

	OnThreadEnd
	(
		function() : ( cleanupData )
		{
			thread CleanupForced1PSequence( cleanupData )
		}
	)

	FirstPersonPlayAnim( sequence, player, cleanupData.player, other )

	WaitFrame()

	while ( player.Anim_IsActive() )
		WaitFrame()
}

void function CleanupForced1PSequence( Forced1PSequenceData cleanupData )
{
	if ( IsValid( cleanupData.player ) )
	{
		cleanupData.player.Show()
		cleanupData.player.ClearViewEntity()
	}

	if ( IsValid( cleanupData.camera ) )
		cleanupData.camera.Destroy()

	if ( IsValid( cleanupData.ownerProxy ) )
	{
		cleanupData.ownerProxy.kv.VisibilityFlags = ~ENTITY_VISIBLE_TO_EVERYONE
		cleanupData.ownerProxy.Die( null, null, { forceKill = true, scriptType = DF_EXPLOSION, damageType = DMG_REMOVENORAGDOLL } )
	}

	if ( IsValid( cleanupData.thirdPersonProxy ) )
	{
		cleanupData.thirdPersonProxy.kv.VisibilityFlags = ~ENTITY_VISIBLE_TO_EVERYONE
		cleanupData.thirdPersonProxy.Die( null, null, { forceKill = true, scriptType = DF_EXPLOSION, damageType = DMG_REMOVENORAGDOLL } )
	}
}

void function SetBodyGroups( entity player, entity proxy )
{
	player.EndSignal( "OnDestroy" )
	player.EndSignal( "OnDeath" )

	proxy.EndSignal( "OnDestroy" )
	proxy.EndSignal( "OnDeath" )

	while ( true )
	{
		proxy.SetFullBodygroup( player.GetFullBodygroup() )

		WaitFrame()
	}
}

void function FirstPersonPlayAnim( FirstPersonSequenceStruct sequence, entity proxy, entity player, entity other )
{
	bool savednoparent = sequence.noParent
	string savedattachment = sequence.attachment
	bool saveduseanimatedrefattachment = sequence.useAnimatedRefAttachment

	if ( !IsPlayerEmbarking( player ) )
	{
		sequence.noParent = false
		sequence.attachment = "REF"
		sequence.useAnimatedRefAttachment = true

		thread FirstPersonSequence( sequence, proxy, player )
	}
	else
		thread FirstPersonSequence( sequence, proxy, other )

	sequence.noParent = savednoparent
	sequence.attachment = savedattachment
	sequence.useAnimatedRefAttachment = saveduseanimatedrefattachment
}

bool function ShouldRunFirstPersonSequence( entity player, bool isEmbark, bool isExecution )
{
	if ( !player.IsPlayer() || player.IsBot() )
		return false

	if ( ( isEmbark && GetUserInfoKVBool_Internal( player, "embark_style", false ) ) || ( isExecution && GetUserInfoKVBool_Internal( player, "execution_style", false ) ) )
		return true
	
	return false
}
#endif

#if CLIENT
void function ServerCallback_HideHudForFPHackAnim()
{
	int ruiDrawIsEnabled = GetConVarInt( "rui_drawEnable" )

	if ( !ruiDrawIsEnabled )
		return

	SetConVarInt( "rui_drawEnable", 0 )

	thread EnableHudOnViewRestored( ruiDrawIsEnabled )
}

void function EnableHudOnViewRestored( int ruiDrawIsEnabled = 1 )
{
	while ( GetViewEntity() != GetLocalClientPlayer() )
		WaitFrame()

	SetConVarInt( "rui_drawEnable", ruiDrawIsEnabled )
}
#endif

#if SERVER
void function ModdedPhaseEmbarkPhaseStart( entity player )
{
	PlayPhaseShiftDisappearFX( player )
	EmitSoundOnEntity( player, "pilot_phaseembark_activate_3p" )	
	player.PhaseShiftBegin( 0.0, 0.2 )

	if ( !IsPlayerEmbarking( player.GetOwner() ) )
	{
		player.GetOwner().PhaseShiftBegin( 0.0, 1.5 )
		AddAnimEvent( player.GetOwner(), "phase_shift_stop", ModdedPhaseEmbarkPhaseStop )
		thread ModdedPhaseExecutionPhaseCleanup( player.GetOwner() )
	}
	
	thread ModdedPhaseEmbarkPhaseCleanup( player )
}

void function ModdedPhaseEmbarkPhaseCleanup( entity player )
{
	EndSignal( player, "OnDeath" )

	OnThreadEnd(
	function() : ( player )
		{
			if ( IsValid( player ) && player.GetModelName() != $"models/robots/spectre/imc_spectre.mdl" && player.GetModelName() != $"models/titans/buddy/titan_buddy.mdl" )
	   			player.SetBodygroup( player.FindBodyGroup( "head" ), 1 )
		}
	)

	WaitSignal( player, "PhaseEmbarkPhaseStop" )
}

void function ModdedPhaseExecutionPhaseCleanup( entity player )
{
	EndSignal( player, "OnDeath" )

	OnThreadEnd(
	function() : ( player )
		{
			if ( IsValid( player ) )
	   			player.Hide()
		}
	)

	WaitSignal( player, "PhaseEmbarkPhaseStop" )
}

void function ModdedPhaseEmbarkPhaseStop( entity player )
{
	Signal( player, "PhaseEmbarkPhaseStop" )
	PlayPhaseShiftDisappearFX( player )
	EmitSoundOnEntity( player, "pilot_phaseembark_end_3p" )
	player.PhaseShiftCancel()
}

void function ModdedPhaseShiftExplodeSound( entity ent )
{
	if ( ent.IsMechanical() )
	{
		EmitSoundOnEntity( ent, "Pilot_Mvmt_Execution_Phaseshift_Pt4_Explo_Robo_3P" )
	}
	else
	{
		EmitSoundOnEntity( ent, "Pilot_Mvmt_Execution_Phaseshift_Pt4_Explo_3P" )
	}
}

void function ModdedAnimEvent_Cloak_On( entity player )
{
	EmitSoundOnEntity( player, "cloak_on_3P" )
	EmitSoundOnEntity( player, "cloak_sustain_loop_3P" )

	float cloakDuration = 1.5 - 0.35
	
	player.SetCanCloak( true )

	player.SetCloakDuration( 0.35, cloakDuration, 1.0 )
}

void function ModdedAnimEvent_Cloak_Off( entity player )
{
	StopSoundOnEntity( player, "cloak_sustain_loop_1P" )
	StopSoundOnEntity( player, "cloak_sustain_loop_3P" )

	bool wasCloaked = player.IsCloaked( CLOAK_INCLUDE_FADE_IN_TIME )

	if ( wasCloaked )
	{
		EmitSoundOnEntity( player, "cloak_interruptend_3P" )

		StopSoundOnEntity( player, "cloak_warningtoend_1P" )
		StopSoundOnEntity( player, "cloak_warningtoend_3P" )
	}

	player.SetCloakDuration( 0, 0, 0.35 )
}
#endif