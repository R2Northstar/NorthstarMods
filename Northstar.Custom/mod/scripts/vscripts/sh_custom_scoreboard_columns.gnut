#if CLIENT
global function ClientColumnData_Init
#endif //CLIENT

#if SERVER
global function AddServerScoreboardColumn
global function ServerColumnData_Init
global function AddServerScoreboardColumnAtRunntime
#endif //SERVER

//matches the GAMETYPE string the amount of vanilla columns they have 
global table<string,int> gamemodeColumns = {    
    [GAMEMODE_CHAMBER] =  2, //One in the chamber
    [GAMEMODE_FASTBALL] = 3, //Fatball
    [GAMEMODE_GG] = 2, //Gun Game
    [GAMEMODE_HIDDEN] = 2, //Hidden
    [GAMEMODE_HIDEANDSEEK] = 0, //Hide and seek
    [GAMEMODE_INFECTION] = 1, //infection
    [GAMEMODE_KR] = 2, // killrace
    [GAMEMODE_SNS] = 3, //Sticks and stones
    [GAMEMODE_TFFA] = 2, //Titan free for all
    [GAMEMODE_CTF_COMP] = 4, //competetive capture the flag
    [GAMEMODE_TT] = 3 , //Titan tag
    ["cp"] = 3, //amped hardpoint
    ["lts"] = 2, //last titan standing
    ["aitdm"] =  3, //attrition
    ["tdm"] = 3,  //skrimish
    ["coliseum"] = 1 , //coliseum
    ["ctf"] = 3, // catpture the flag
    ["ffa"] = 3, //free for all
    ["speedball"] = 3, //live fire
    ["ttdm"] = 3  //titan brawl
}


#if CLIENT

void function ClientColumnData_Init()
{
    AddServerToClientStringCommandCallback( "AddColumnToClient", ServerCallback_AddColumnToClient ) //add fucntion to client
}

void function ServerCallback_AddColumnToClient( array<string> args ) //define the function that was added
{
    GameMode_AddScoreboardColumnData( args[0], RetrunSpace(args[1]), args[2].tointeger(), args[3].tointeger() )
}

#endif //CLIENT

#if SERVER

struct ColumnData{
    string gamemode //Technically oboslete but saver this way 
    string scoreName //What the player sees on the score board
    int scoreType //The PGS constant that is shown, technically this can be a random one, just make sure its not in use
    int digits //The determines the width of the colum, I dont know if there is a limit, would make sense if there was
}
// array of all the custom columns
array<ColumnData> columns 

void function ServerColumnData_Init()
{
    AddCallback_OnClientConnected( AddColumnOnConnect )
}

void function AddServerScoreboardColumn(string scoreName, int scoreType, int digits)
{   
    if(GAMETYPE in gamemodeColumns && gamemodeColumns[GAMETYPE] + columns.len() >= 4) // this just checks if there are already 4 columns
        throw "Cannot have more than 4 total columns"
    //Looks terrible, but this is the only way to define a struct in a function with args
    AddColumnToArray(scoreName, scoreType, digits)
}

void function AddColumnToArray(string scoreName, int scoreType, int digits)
{
    ColumnData column = {...}
    column.gamemode = GAMETYPE // GAMETYPE returns the string identifier, e.g. "gg" for gun game
    column.scoreName = ReplaceSpace(scoreName) //get rid of spaces now, revert them later
    column.scoreType = scoreType
    column.digits = digits
    foreach(ColumnData cdat in columns)
        if(cdat.scoreName == column.scoreName)
            throw "Cannot have 2 or more columns with the same name"

    columns.append(column)
}

//adds all the registered columns when a player connects
void function AddColumnOnConnect(entity player)
{
    foreach(ColumnData cdat in columns)
	    ServerToClientStringCommand(player,"AddColumnToClient " + cdat.gamemode + " " + cdat.scoreName + " " +cdat.scoreType+ " " + cdat.digits)
}

void function AddServerScoreboardColumnAtRunntime(string scoreName, int scoreType, int digits)
{
    // this just checks if there are already 4 columns
    if(GAMETYPE in gamemodeColumns && gamemodeColumns[GAMETYPE] + columns.len() >=4 ) 
       throw "Cannot create more than 4 Columns"
    // add it to the array for players that join later 
    AddColumnToArray(scoreName, scoreType, digits)
    // add it to the currently existing players
    foreach(entity p in GetPlayerArray())
        ServerToClientStringCommand(p,"AddColumnToClient " + GAMETYPE + " " + ReplaceSpace(scoreName) + " " + scoreType+ " " + digits)
}

#endif //SERVER

// shared functions
string function ReplaceSpace(string s)
{
    return StringReplace(s," ","x!c",true, true) //used as a replacement for string, as the args are seperated by spaces, this later gets reverted
}
string function RetrunSpace(string s)
{
    return StringReplace(s,"x!c"," ",true, true) // reverts the changes to get back the spaces
}